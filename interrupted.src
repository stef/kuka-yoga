&ACCESS RVO
&REL 110
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM EDITMASK = *
DEF interrupted( )
DECL AXIS ARP[4], AHP[16] ; attack start & end positions ARP=Attack Robot Position, AHP=Attack Human Positions 
DECL AXIS HOME
DECL MODUS_T MR_T
DECL STATE_T SC_T

;FOLD INI
  ;FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3 
    BAS (#INITMOV,0 )
    HOME={AXIS: A1 0, A2 -90, A3 90, A4 -90, A5 -90, A6 90}

  ;ENDFOLD (BASISTECH INI)
  ;FOLD USER INI
    ;Make your modifications here
    GLOBAL INTERRUPT DECL 10 WHEN $DATA_SER3<>0 DO HANDLER()
  ;ENDFOLD (USER INI)
;ENDFOLD (INI)
ATTACK_ROBOTS=FALSE
RNG=1337
$ADVANCE=0

PTP HOME; BCO run

;FOLD DEFINE_AHP
  AHP[1]={A1 60, A2 -50, A3 90, A4 -90, A5 -90, A6 60}
  AHP[2]={A1 55, A2 -40, A3 10, A4 -90, A5 -90, A6 120}
  AHP[3]={A1 -65, A2 -40, A3 10, A4 -90, A5 -90, A6 125}
  AHP[4]={A1 -75, A2 -50, A3 90, A4 -90, A5 -90, A6 50}
  AHP[5]={A1 125, A2 -60, A3 80, A4 -100, A5 -90, A6 70}
  AHP[6]={A1 115, A2 -50, A3 90, A4 -90, A5 -90, A6 60}
  AHP[7]={A1 110, A2 -40, A3 10, A4 -90, A5 -90, A6 120}
  AHP[8]={A1 -145, A2 -40, A3 10, A4 -90, A5 -90, A6 125}
  AHP[9]={A1 -155, A2 -50, A3 90, A4 -90, A5 -90, A6 50}
  AHP[10]={A1 145, A2 -60, A3 80, A4 -100, A5 -90, A6 70}
  AHP[11]={A1 150, A2 -50, A3 90, A4 -90, A5 -90, A6 60}
  AHP[12]={A1 148, A2 -40, A3 10, A4 -90, A5 -90, A6 120}
  AHP[13]={A1 -130, A2 -60, A3 80, A4 -100, A5 -90, A6 70}
  AHP[14]={A1 -120, A2 -50, A3 90, A4 -90, A5 -90, A6 60}
  AHP[15]={A1 -125, A2 -40, A3 10, A4 -90, A5 -90, A6 120}
  AHP[16]={A1 50, A2 -60, A3 80, A4 -100, A5 -90, A6 70}
;ENDFOLD (DEFINE_AHP)

;FOLD DEFINE_ARP
  ARP[1]={A1 45, A2 -130, A3 140, A4 -90, A5 -40, A6 80}
  ARP[2]={A1 0, A2 -130, A3 140, A4 -90, A5 -90, A6 80}
  ARP[3]={A1 0, A2 -70, A3 140, A4 -90, A5 -90, A6 15}
  ARP[4]={A1 40, A2 -110, A3 110, A4 -90, A5 -40, A6 90}
;ENDFOLD (DEFINE_ARP)

COPEN(:SER_3,HANDLE)
IF(HANDLE==0) THEN
    HALT
ENDIF
INTERRUPT ON
LOOP
  IF ATTACK_ROBOTS == TRUE THEN
    MR_T=#SYNC
    CWRITE(HANDLE, SC_T, MR_T, "1")
    PTP ARP[(RANDOM() B_AND 3)+1]
    ;FOLD WAIT Time= 1 sec;%{PE}%R 5.4.35,%MKUKATPBASIS,%CWAIT,%VWAIT,%P 2:1
    WAIT SEC 1
    ;ENDFOLD
    CWRITE(HANDLE, SC_T, MR_T, "0")
  ELSE ; ATTACK HUMANS. ARRRR
    MR_T=#SYNC
    CWRITE(HANDLE, SC_T, MR_T, "1")
    PTP AHP[(RANDOM() B_AND 15)+1]
    CWRITE(HANDLE, SC_T, MR_T, "0")
  ENDIF
ENDLOOP
END

DEF HANDLER()
  INT OFFSET, BCI
  DECL MODUS_T MR_T
  DECL STATE_T SC_T
  REAL TIMEOUT

  MR_T=#ABS
  TIMEOUT=0.1
  OFFSET=0

  CREAD(HANDLE, SC_T, MR_T, TIMEOUT, OFFSET, "%1r", BCI);
  SWITCH SC_T.RET1
  CASE #CMD_OK
    HALT
  CASE #CMD_TIMEOUT
     HALT
  CASE #DATA_OK
     HALT
  CASE #DATA_BLK
     HALT
  CASE #DATA_END
     IF 'H34' == BCI THEN
        ATTACK_ROBOTS=FALSE
     ELSE 
        IF 'H37' == BCI THEN
           ATTACK_ROBOTS=TRUE
        ELSE
           HALT
        ENDIF
     ENDIF
  CASE #CMD_ABORT
     HALT
  CASE #FMT_ERR
     HALT
  ENDSWITCH
END

DEFFCT INT RANDOM() ; RANDOM() B_AND 15
  INT X
  X=RNG
  X=X B_EXOR (X*8192)
  X=X B_EXOR (X/131072)
  X=X B_EXOR (X*32)
  RNG=X
  RETURN RNG
ENDFCT
